#!/bin/sh
#|-*- mode:lisp -*-|#
#| <Put a one-line description here>
exec ros dynamic-space-size=16000 -Q -- $0 "$@"
|#

#|

This is a failed attempt to learn the action model from the symbolic transitions,
using a data mining technique called MSDD.

Oates, Tim, and Paul R. Cohen. "Searching for structure in multiple streams of data." ICML. Vol. 96. 1996.

|#



;;; setup
(progn ;;init forms
  #+quicklisp (ql:quickload '(iterate alexandria trivia.ppcre immutable-struct swank
                              priority-queue simpsamp lparallel) :silent t)
  )

(defpackage :msdd
  (:use :cl :iterate :alexandria :trivia :trivia.ppcre :priority-queue :lparallel)
  (:shadowing-import-from :immutable-struct :ftype :defstruct))
(in-package :msdd)
(SETF *ARITY-CHECK-BY-TEST-CALL* NIL)
;; (setf (sb-ext:bytes-consed-between-gcs) 100000000)
(defvar *num-processes*
    (read-from-string
     (uiop:run-program "cat /proc/cpuinfo | grep -c processor" :output :string)))
;; (setf *kernel* (make-kernel *num-processes*))
(defmacro print-variables (&rest args)
  `(progn
     (print (list ,@(iter (for arg in args)
                          (collect `(quote ,arg))
                          (collect arg)))
            *error-output*)
     (finish-output *error-output*)))
;;; set up the learning data

(defun read-as-bvs (csv)
  (iter (for line in-file csv using #'read-line)
        (for bv = (iter (for o in-stream (make-string-input-stream line))
                        (collect o result-type bit-vector)))
        (when (plusp (length bv))
          (collect bv))))

(defun bvs-array (bvs)
  "Stores the bit vectors into a single large bit vector, then
 returns an array displaced to the underlying bitvector"
  (let ((batch (length bvs))
        (w (/ (length (first bvs)) 2)))
    (let* ((arm (make-array (* batch 2 w)  :element-type 'bit)))
      (iter (for b below batch)
            (for bv in bvs)
            (replace arm bv :start1 (* b 2 w) :end1 (* (1+ b) 2 w)))
      arm)))

;;; print the pddl domain

(defclass _nil () ())
(defun _nil () (make-instance '_nil))
(defmethod print-object ((object _nil) stream)
  (write-string "()" stream))

(defun sym (&rest args)
  (intern (format nil "狺狎珞┅ㄤ彐躅ㄤ轫癌蝈趱蝾骘蝽ㄚ癌犷犰殡澧扉篝簌溟愆┅ㄤ彐躅磲脲滹磲轭ㄤ狒岍戾舄è溟ǒ戾铉翳ㄦ轵篝溽翎┅博ㄤ轫ㄩ雉溟愆┅啜溴骈铄ㄤ镯衢灬翦铘ê蝈聃轵屙孱趔后趄轲侯彗狒轹瀛痱邈镱溟糸镱螬ê痱邃殂狒弩括磲疸狎＇溟眢┅括轸弪ㄦ矧趄犷箝糸镱轭溽翎ㄦ矧骝镯篚怏羼趄犷箝糸镱溟愆ㄦ矧麸篚怏羼趄犷箝糸镱溟愆ㄦ矧骝镯癌ㄣ镬戾泗啜横泗轱簌пㄡ痧禊＇泔钽狒孱狒篝蜷铉磲ъ轶＇痱轭悱麸篝蜷铉趄犷箝糸镱┅吼狎犴弭弪ㄟ铋飑辉夏虾审轸痫篌殁戾麸躞疳蜥礤翦蝮轭秕骝犴鬻矧肟吼蝈泔钿轸轱ㄡ钿括轸弪ㄦ矧轭溟眢ㄦ矧鲠祯轭鲥泗矧骝镯磲翥鲠祯ú铋飑ūㄣ镬戾泗洎┅òㄣ镬戾泗啜铒洎┅┅┅哄骀邈ㄡ钿括轸弪ㄦ矧轭溟眢ㄦ矧鲠祯灞轭鲥泗矧骝镯ㄦ矧鲠祯宀轭鲥泗矧麸ㄥ磲翥瑾鲠祯灞鲠祯宀è博铋飑è暴ㄣ镬戾泗洎┅è癌ㄣ镬戾泗啜铒洎┅è癌铋飑è暴铋飑è暴ㄣ镬戾泗洎┅è癌ㄣ镬戾泗啜铒洎┅┅┅┅┅┅换箦狎汨溽翎篝蝓泗躜ㄤ彐豉疱箧ī箝铉戾骒镝舂ㄤ彐豉疱骖īф轼铛愆ㄤ彐豉疱栳戽ī啜躅箝珙邃怡翦ūǒㄦ祜矧祜盹篝痫箝糸鲥骈铛博博┅ㄤ彐躅眭祠轸镫孱鏖漪瑭磲脲狎蜥鏖漪洪铋糸犰屐屙孱哄戾礤铘豉疱р轸横潢躞翎忪铋烘殪飙痫轭翦铋飑ㄤ彐躅泔瘗眭祠轸镫孱眙暴戾è眙眭祠轸镫孱戾铉翳眙暴┅蝈痨徙眙眙暴眙博ㄤ彐躅蝈聃轵邃īㄥ蝌矧㈨轶箝铉蝈聃轵邃狎珲礤铘┅ㄤ彐篝蝓泗铒溴换痱邈躜箫蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧换篚沣弩箫蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧换痱邈躜箫颡蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧换篚沣弩箫颡蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧ㄤ狒蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧磲箅蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧┅ㄤ彐躅狍氨铒溴ㄥ磲翥铒溴è铒溴溽翎磲箅磲鲥泗矧灬礅溽磲翥瑾è暴博è癌洎溽翎磲箅┅┅换忾舡鲥泗矧豸殪轸殄ㄤ彐磲泸潇弭ㄢ轭溟铉怙澌怙澌啜戾忾钿轭珞ㄤ邈灬蝈ㄤ钺黹悱屮翦铘括磲疸狎ㄣ镯痫箦＇骈蝮＇孱篚蝈扉篝忾钿轭珞┅棱镤┅ㄤ彐磲泸潇弭ㄢ轭溟铉怙澌怙澌啜戾舄忾钿轭珞ㄤ邈灬蝈ㄤ钺黹悱屮翦铘括磲疸狎ㄣ镯痫箦＇骈蝮＇孱篚蝈扉篝忾钿轭珞┅棱镤┅ㄤ邈灬轫ㄩ铎轭磲脲忾舡鲥泗矧┅ㄦ豉疱磲脲忾舡鲥泗矧栳戽箝眇戾忾舡鲥泗矧ㄤ彐躅磲脲忾舡鲥泗矧戾铉翳磲脲狎蜥戾铉翳哄戾礤铘豉疱р轸┅ㄤ邈灬轫ㄩ铎轭忾舡殒翳孱屐箦┅ㄦ豉疱忾舡殒翳孱屐箦忾舡鲥泗矧忾舡鲥泗矧忾舡鲥泗矧镳糸镱犰忾舡鲥泗矧忾舡鲥泗矧ㄤ彐躅忾舡殒翳孱屐箦ㄣ镱溟糸镱翳孱屐箦镳糸镱犰蝈磲脲忾舡鲥泗矧戾铉翳泔钿轸轱瞟┅㈤翳孱屐箦恽ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅ㄤ戾è繇磲脲忾舡鲥泗矧戾铉翳泔钿轸轱瞟┅ㄢ轸犷翳孱泔钿轸轱繇皓ㄢ轸犷溷屐箦泔钿轸轱蝈螬ㄢ轸轱繇蝈蝈螬蝈螬ㄡ篌弪ㄥ聃犰ㄢ轸殒翳孱屐箦＊鞍鞍北北＊鞍北鞍北＊氨氨氨氨＊氨氨鞍北┅ㄤ邈灬轫ㄩ铎轭忾舡轫痨殄螬ㄦ豉疱忾舡轫痨殄忾舡鲥泗矧忾舡鲥泗矧镳糸镱犰忾舡鲥泗矧忾舡鲥泗矧ㄤ彐躅忾舡轫痨殄ㄢ霰怫镳糸镱犰ㄢ龀磲脲狎蜥戾铉翳怫暴哄戾礤铘豉疱р轸┅⑨骄涵铒矧猗ㄢ轸铒怫怫畅ㄢ轸轱怫怫怫畅怫畅换雉桢豸殪ㄤ彐磲泸滹糸礤蟓轭扉铄è鲠泔躅镳糸镱犰蝈篚祠骘蝽怙澌怙澌孱鲩蝻铐孱孱雯ㄣ桢汶豉疱鲠簌礅镬戾è泔躅磲泸镥疳钿泔躅孱雯┅ㄡ篌弪ㄡ钿ㄣ镱篝犷麴泔躅舂铛礅弪泔躅舂┅ㄩ翦ㄦ矧麸泔躅舂麒孱ㄦ轵篝轸弪狒轱瞽皓ㄣ镬戾泗ю蝻珙┅ㄣ镬戾泗ㄩ泔躅舂啜簌礅镬磲泸镬弭è鲠悌棱镤蝈篚祠骘蝽┅┅ㄤ彐磲泸滹糸礤蟓躅蝻祆è忉箦镦骟弭泔躅躅蝻祆镳糸镱犰蝈篚祠怙澌怙澌孱鲩蝻铐孱孱雯鏖翳珏铙眢聃狒盹铘璀祜镳戾è躅蝻祆磲泸镥疳钿躅蝻祆孱雯ㄤ屐翎ㄧ孱簌⒛┅啜戾è忉箦癌ㄤ邈灬蝈ㄦ轼铛忉箦┅眭祠轲戾鲠祯瀛忾钿ì聃狒盹洎ㄦ祜矧泔躅躅蝻祆ㄤ邈灬蝈ㄦ轼铛聃狒è盹躅蝻祆盹洎簌礅镬磲泸镬弭è溴祠躅蝻祆┅ㄤ雉轫弩ì铘璀祜镳聃狒ㄤ邈灬蝈ㄩ珙矧徕戾铘璀祜镳┅ㄤ雉轫弩轭扉铄ì镦骟弭躅蝻祆棱镤ㄩ钽忉箦溴祠岍┅簌礅镬磲泸镬弭è溴祠暴ì镦骟弭癌ㄤ雉轫弩ì铘璀祜镳盹洎棱镤ㄩ钽忉箦溴祠岍┅蝈篚祠┅┅换铒溴弼犰踽糸镱骢钽糸镱ㄤ邈灬轫ㄩ铎轭鏖汶孱蟓泔镢沲蝈钽瀛绌ㄦ豉疱鏖汶孱蟓泔镢沲蝈钽瀛蝈犰蝈犰蝈犰蝈犰箧ㄤ彐躅鏖汶孱蟓泔镢沲蝈钽瀛畋畈畛畲骑祆秣轭翳溴骈铋糸镱轭厢翦蟋蔑桢瞵擅吞苟渝犰箫组汶孱蟋援漠惫腹王祠轺狴蔑铘轭珏钽葬忪弩令犰箝骘翳语汩犰鱼殄钽弩提黩孱沐膨焘狨馏箫汩狒弩戾舄è畋ㄦ祜狒畋┅畈ㄦ祜狒畈┅畛ㄦ祜狒畛┅畲ㄦ祜狒畲┅ū犰ǒǐ畋畈畛畲┅虮ǐ畋畈┅ㄣǐ畋畛┅虿ǐ畛畲┅ㄣǐ畈畲┅畋í虮惚悲犰飑畈í虮悴悲犰飑畛í虿惚悲犰飑畲í虿悴悲犰飑┅í换徜溟铉卞麸痱弼孱溟鲩箝镱怡弪ǐí畋祜ǒǐ卞畋ǐ卞畋蕞┅í畈祜ǒǐ卞畈ǐ卞畈蕞┅í畛祜ǒǐ卞畛ǐ卞畛蕞┅í畲祜ǒǐ卞畲ǐ卞畲蕞┅┅┅ㄤ邈灬轫ㄩ铎轭泸矬蟓孱趄镳┅ㄦ豉疱泸矬蟓孱趄镳蝈犰蝈犰蝈犰蝈犰箧ㄤ彐躅泸矬蟓孱趄镳畋畈畛畲⒄箝铉翳轶轭篝遽镦鏖汶孱蟓泔镢沲蝈钽瀛忮汜躞翳轶鲠祯轶铒蝽犰辁邃徵衢铙翳麸翎筢眇戾箝瀹戾舄è畋ㄦ祜狒畋┅畈ㄦ祜狒畈┅畛ㄦ祜狒畛┅畲ㄦ祜狒畲┅ū犰ǒǐ畋畈畛畲┅虮ǐ畋畈┅ㄣǐ畋畛┅虿ǐ畛畲┅ㄣǐ畈畲┅畋í虮惚悲犰飑畈í虮悴悲犰飑畛í虿惚悲犰飑畲í虿悴悲犰飑┅í悲犰换徜溟铉卞麸痱弼孱溟鲩箝镱怡弪ǐí畋祜ǒǐ卞畋ǐ卞畋蕞┅í畈祜ǒǐ卞畈ǐ卞畈蕞┅í畛祜ǒǐ卞畛ǐ卞畛蕞┅í畲祜ǒǐ卞畲ǐ卞畲蕞┅┅┅ㄦ豉疱眢滗珥狲骖骖骖骖箧ㄤ彐躅眢滗珥狲畋畈畛畲磲ㄣ蝻篌孱趄镳畋畈ǐ畛畲┅ㄣ蝻篌孱趄镳ǐ畋畈畛畲┅ㄦ豉疱眢滗珥狲骖骖骖骖箧ㄤ彐躅眢滗珥狲畋畈畛畲磲戾è繇鸨ǐ畈畛畲┅ㄩ冀畋繇鸨ㄣ蝻篌孱趄镳畋繇鸨戾è犰殳ǒǐ畋繇鸨博┅ㄣ蝻篌孱趄镳犰殳犰殳博┅ㄣ镱è窘畋ㄡ怏ō畈畛┅戾è繇鸩ǐ畋畈畛┅ㄣ蝻篌孱趄镳繇鸩繇鸩畲┅è畈畛ㄣ蝻篌孱趄镳畈ǐ畋畛畲┅ㄣ蝻篌孱趄镳ǐ畋畈畛畲┅┅ㄦ豉疱眢滗珥狲铒溴骖骖骖骖箧ㄤ彐躅眢滗珥狲铒溴畋畈畛畲ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅磲翥铒溴è铒溴磲箅ㄤ戾舄è鏖漪璨戾铉翳磲箅┅鏖漪ǒ鏖漪璨博篚沣弩箫颦磲脲忾舡鲥泗矧鏖漪瑭┅ㄤ邈灬蝈ㄨ犰鏖漪璨┅蝈痨徙篚沣弩箫颦磲箅后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨ㄩ铒弪镳ㄣ秕铘篚沣弩箫颦┅换铒瞽屙痿篚沣弩箫眢滗珥狲畋畈畛畲换屙痿篚沣弩箫眢滗珥狲畋畈畛畲┅┅┅ㄦ豉疱泔铘轭珏钽翎忪栳戽栳戽铒溴箝眇戾忾舡鲥泗矧鲠祯弩骖骖骖骖┅ㄤ彐躅泔铘轭珏钽翎忪ㄢ狒汨鏖漪铒溴狎蜥ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅ㄥ磲翥瑾铒溴è铒溴溽翎磲箅┅ㄤ戾舄è畋癌畈癌畛癌畲癌鏖漪璨í鏖漪瑭痱邈躜箫磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫磲脲忾舡鲥泗矧鏖漪瑭痱邈躜箫颦磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫颦磲脲忾舡鲥泗矧鏖漪瑭弪磲脲忾舡鲥泗矧鏖漪瑭镱磲脲忾舡鲥泗矧鏖漪瑭ㄢ彐矧瀛篝狒磲脲忾舡鲥泗矧鏖漪瑭ㄡ骠弪篝狒磲脲忾舡鲥泗矧鏖漪瑭痱邈磲翥栝铉磲脲忾舡鲥泗矧鏖漪瑭篚沣磲翥栝铉磲脲忾舡鲥泗矧鏖漪瑭┅ㄤ邈灬蝈ㄦ轼铛畋畈畛畲ㄨ犰鏖漪璨┅蝈痨徙痱邈躜箫溽翎后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫溽翎后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨蝈痨徙痱邈躜箫颦磲箅后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫颦磲箅后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨ㄦ殪弪癌ㄦ殪镱暴ㄤ雉轫弩躅蝻祆ㄢ镦骟弭忉翥博戾舄è楸翳骈铛í翳骈铛ǐ镦骟弭┅鏖漪璨┅ㄩ翳骈铛ǐ楸鏖漪瑭┅ㄩ翳骈铛ǐ椴鏖漪瑭┅蝈痨徙忮骘蝈篝狒狎蜥后翎螋楸哄钿椴蝈痨徙徭翦颦篝狒狎蜥后翎螋椴哄钿槌┅换噤镱舡汜蝈痱邈躜箫蝮磲翥徵衢铙犷鲠祯弩ㄢ轸羼忮骘蝈篝狒痱邈躜箫痱邈磲翥栝铉ㄢ轸轱痱邈磲翥栝铉痱邈躜箫颦痱邈磲翥栝铉换噤镱舡汜蝈彐驽泗磲翥徵衢铙犷鲠祯弩ㄢ轸羼徭翦颦篝狒篚沣弩箫篚沣磲翥栝铉ㄢ轸轱篚沣磲翥栝铉篚沣弩箫颦篚沣磲翥栝铉ㄥ磲翥瑾è羼踽镱痱邈磲翥栝铉ㄥ聃犰镱篚沣磲翥栝铉┅è舂ㄩ钽畋┅è铋飑ㄩ钽畈┅è铋舂ㄩ钽畛┅è铋铋飑ㄩ钽畲┅┅鲠祯弩畋畈畛畲┅┅换序躅轭蝓戾骘徙糸镱筱桢磲ㄦ豉疱鲠扉洵铒溴铒溴栳戽怙镬遽瞟ㄤ彐躅鲠扉洵铒溴铒溴鏖漪瑭ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅磲翥铒溴è铒溴溽翎磲箅ㄤ戾舄è鏖漪璨í鏖漪瑭痱邈躜箫磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫磲脲忾舡鲥泗矧鏖漪瑭痱邈躜箫颦磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫颦磲脲忾舡鲥泗矧鏖漪瑭弪磲脲忾舡鲥泗矧鏖漪瑭镱磲脲忾舡鲥泗矧鏖漪瑭┅蝈痨徙痱邈躜箫溽翎后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫溽翎后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨蝈痨徙痱邈躜箫颦磲箅后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫颦磲箅后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨ㄦ殪弪癌ㄦ殪镱暴ㄡ钿ㄤ戾è繇磲脲忾舡鲥泗矧鏖漪瑭┅换碑筢礤忾箬秕熹铒箴邈殒翳筢礤鲠祯鸫戾骠疳虺ㄢ轸羼痱邈躜箫篚沣弩箫繇皓筢礤忾趔珏被犰忾趔箬秕熹忮ㄢ轸犷溷繇痱邈躜箫颦繇皓栾麇鲥颥磲箅邃忾趔狎镫ㄢ轸犷溷繇篚沣弩箫颦繇皓筢礤骘篚沣弩箫蝮换犰弪ㄥ聃犰弪繇皓┅┅┅ㄦ豉疱鲠扉洵戾徭铒溴铒溴栳戽怙镬遽瞟ㄤ彐躅鲠扉洵戾徭铒溴铒溴鏖漪瑭ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅磲翥铒溴è铒溴溽翎磲箅ㄤ戾舄è鏖漪璨í鏖漪瑭痱邈躜箫磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫磲脲忾舡鲥泗矧鏖漪瑭痱邈躜箫颦磲脲忾舡鲥泗矧鏖漪瑭篚沣弩箫颦磲脲忾舡鲥泗矧鏖漪瑭弪磲脲忾舡鲥泗矧鏖漪瑭镱磲脲忾舡鲥泗矧鏖漪瑭┅蝈痨徙痱邈躜箫溽翎后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫溽翎后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨蝈痨徙痱邈躜箫颦磲箅后翎螋哄钿鏖漪后翎螋哄钿鏖漪瑭蝈痨徙篚沣弩箫颦磲箅后翎螋哄钿鏖漪后翎螋鏖漪哄钿鏖漪璨ㄦ殪弪癌ㄦ殪镱暴ㄡ钿换篚沣弩箫箬秕熹箴邈殒狒戾狍镱忾ㄤ戾ī铒ㄥ聃犰镱篚沣弩箫颦┅ㄤ戾è繇鸨磲脲忾舡鲥泗矧鏖漪瑭繇鸩磲脲忾舡鲥泗矧鏖漪瑭繇鸪磲脲忾舡鲥泗矧鏖漪瑭┅换伯徙糸镱箬秕熹孱泔溴汨犷珏换浇麒孱翳彐驽泗箴邈殒殄箫礤翳轭ò┈翳孱翳痱邈躜箫箬秕熹忮箴邈殒殄ò换犷轸箬秕熹忮溟骀弪孱ㄢ轸铒痱邈躜箫颦繇鸨ㄢ轸铒篚沣弩箫颦繇鸩ㄢ轸轫痨殄繇鸩繇鸨繇鸪ㄥ聃犰镱繇鸪┅┅┅换陀哪犰顼蜷翳ㄦ豉疱屮疳钿铒溴扉篝ㄤ彐躅屮疳钿铒溴磲翥铒溴è铒溴溽翎磲箅ㄦ戾è箴邈殒眭祠轸镫孱轭溴麒狒戾è眙ㄣ镳眭祠轸镫孱眭祠轸镫孱┅箦翩ㄡ蝈眙轭溴麒狒眙博┅换箴邈殒忾镦翳痱邈躜箫矧翳篚沣弩箫虍换娱钽翳痱邈躜箫犷翳篚沣弩箫狎翳溟箴灬沐狎蜥麸换箝铉戾躅溴蜢轭溽翎狎蜥换麇汜轸弪狒秭弪翳躅溴蜢轭狎蜥戾è蜷玷繇矬矧痫箝糸镱磲箅烘蝻憝孱舂暴┅ㄩ翦ㄦ矧骝镯ū蜷玷繇矬舂忮祜戾铉翳溽翎┅ㄣ镬戾泗铒溴箴邈殒溽翎癌箴邈殒磲箅癌┅ㄣ镬戾泗铒溴箴邈殒溽翎暴箴邈殒磲箅癌┅┅┅┅ㄤ彐躅痱镧蝈篌鏖漪瑭戾è泔躅癌灬礅溽é镳糸镱犰ㄣ栳＼蝈篝狎珞痱轭汨狎ㄩ钽泔躅舂麒孱鏖漪泔躅舂痱镧箦翩泔躅癌ㄦ矧磲狺%" args)))
      (finish-output))))

;; it seems there are still some magic happening here... 

(defun bistate-msdd (batch width array g-threashold max-schema)
  "A modification of Oates, Cohen, ICML16 on binary variables with lag fixed to 1.
Also, instead of working on the sequence of states, it works on individual pairs of states."
  (let ((open (make-pqueue #'> :key-type 'sf :value-type 'node))
        (acc (make-pqueue #'< :key-type 'sf :value-type 'node))
        (lopen (bt:make-lock "open"))
        (lacc (bt:make-lock "acc"))
        threads)
    (labels ((task (node)
               (block nil
                 (multiple-value-bind (n1 n2 n3 n4)
                     (contingency-table batch width node array)
                   ;; Add the node to the results if they are actually good
                   (let ((g (cross-entropy n1 n2 n3 n4))
                         (f (msdd-gmax3 node n1 n2 n3 n4)))
                     ;; (print-variables n1 n2 n3 n4 g f)
                     (when (and (< g-threashold g)
                                (valid-leaf-node node width))
                       (bt:with-lock-held (lacc)
                         (when (or (< (pqueue-length acc) max-schema)
                                   (< (pqueue-front-key acc) g))
                           (pqueue-push node g acc))
                         (when (< max-schema (pqueue-length acc))
                           (pqueue-pop acc))))
                     ;; Add the children to the queue
                     (when (and (< g-threashold f)
                                (bt:with-lock-held (lacc)
                                  (or (< (pqueue-length acc) max-schema)
                                      (< (pqueue-front-key acc) f))))
                       (let ((children (delete-if-not
                                        (lambda (node) (valid-node node width))
                                        (expand node))))
                         (bt:with-lock-held (lopen)
                           (iter (for child in children)
                                 (pqueue-push child f open)))))))))
             (worker ()
               (sleep (random 1.0))
               (block nil
                 (tagbody
                   :start
                   (task
                    (bt:with-lock-held (lacc)
                      (bt:with-lock-held (lopen)
                        (if (or (< (pqueue-length acc) max-schema)
                                (< (pqueue-front-key acc)
                                   (pqueue-front-key open)))
                            (if (pqueue-empty-p open)
                                (go :retry)
                                (pqueue-pop open))
                            (go :end)))))
                   (go :start)
                   :retry
                   (sleep 0.1)
                   (go :start)
                   :end)))
             (single-thread-worker ()
               (iter (until (pqueue-empty-p open))
                     (while (or (< (pqueue-length acc) max-schema)
                                (< (pqueue-front-key acc)
                                   (pqueue-front-key open))))
                     (for i from 0)
                     (when (zerop (rem i 1000))
                       (let ((ub (pqueue-front-key open))
                             (lb (ignore-errors (pqueue-front-key acc)))
                             (len (pqueue-length open)))
                         (print-variables ub lb len)))
                     (task (pqueue-pop open)))))
      (let ((init (node (multitoken (* 2 width))
                        (multitoken (* 2 width)))))
        (pqueue-push init
                     (multiple-value-call
                         #'cross-entropy
                       (contingency-table batch width init array))
                     open))
      (handler-case
          (unwind-protect
              (if (plusp *num-processes*)
                  (unwind-protect
                      (progn
                        (dotimes (i *num-processes*)
                          (push (bt:make-thread #'worker) threads))
                        (iter (while (some #'bt:thread-alive-p threads))
                              (for i from 0)
                              (let ((ub (pqueue-front-key open))
                                    (lb (ignore-errors (pqueue-front-key acc)))
                                    (len (pqueue-length open)))
                                (print-variables ub lb len))
                              (sleep 1)))
                    (map nil #'bt:destroy-thread threads))
                  (single-thread-worker))
            (print-variables acc))
        (SB-SYS:INTERACTIVE-INTERRUPT ())))
    (iter (until (pqueue-empty-p acc))
          (for (values node g) = (pqueue-pop acc))
          (print-variables g node)
          (collect node))))




(defvar *g-threshold* most-negative-short-float)
(defvar *max-schema* most-positive-fixnum)
(defvar *num-samples* nil)
(setf *print-right-margin* 70)
;; (setf *print-pretty* nil)
(defun main (&rest args)
  (match args
    ((list* (or "-d" "--debug") rest)
     (format *error-output* "debug mode on%")
     (apply #'main "-P" "0" "-s" "2017" "-p" "-t" rest))
    ((list* (or "-g" "--threashold") (read *g-threshold*) rest)
     (check-type *g-threshold* real)
     (apply #'main rest))
    ((list* (or "-s" "--seed") (read seed) rest)
     (check-type seed (unsigned-byte 32))
     (let ((*random-state* (SB-EXT:SEED-RANDOM-STATE seed)))
       (apply #'main rest)))
    ((list* (or "-k" "--max-schema") (read *max-schema*) rest)
     (check-type *max-schema* integer)
     (apply #'main rest))
    ((list* (or "-n" "--num-samples") (read *num-samples*) rest)
     (check-type *num-samples* integer)
     (apply #'main rest))
    ((list* (or "-P" "--num-processes") (read *num-processes*) rest)
     (check-type *num-processes* integer)
     (apply #'main rest))
    ((list* (or "-p" "--profile") rest)
     (swank:profile-reset)
     (swank:profile-package :msdd t nil)
     (sb-profile:unprofile read-as-bvs bvs-array)
     (unwind-protect
         (apply #'main rest)
       (swank:profile-report)))
    ((list* (or "-t" "--time") rest)
     (let ((*trace-output* *error-output*))
       (time (apply #'main rest))))
    ((list csv)
     (let* ((list (read-as-bvs csv))
            (original-batch (length list))
            (width (/ (length (first list)) 2))
            (batch (or *num-samples* original-batch))
            (array (bvs-array
                    (if *num-samples*
                        (simpsamp:list-sample *num-samples* list)
                        list))))
       (format *error-output* "dynamic space: aGB%" (/ (sb-ext:dynamic-space-size) 1e9))
       (format *error-output* "number of threads: a%" *num-processes*)
       (format *error-output* "number of original samples: a%" batch)
       (format *error-output* "encoding length: a%" width)
       (format *error-output* "number of samples: a%" batch)
       (format
        t "%(:S)%"
        (make-domain
         (mapcar #'as-012
                 (bistate-msdd batch width array
                               *g-threshold* *max-schema*))))))
    (_
     (format *error-output*
             "Usage: msdd.ros                      [-g,--threashold threashold]                      [-p,--profile]                      [-t,--time] actions.csv%"))))

;;; vim: set ft=lisp lisp:




