#!/bin/sh
#|-*- mode:lisp -*-|#
#| <Put a one-line description here>
exec ros dynamic-space-size=8000 -Q -L sbcl -- $0 "$@"
|#
(progn ;;init forms
  #+quicklisp (ql:quickload '(iterate alexandria trivia.ppcre immutable-struct swank
                              priority-queue simpsamp lparallel) :silent t)
  )

(defpackage :msdd
  (:use :cl :iterate :alexandria :trivia :trivia.ppcre :priority-queue :lparallel)
  (:shadowing-import-from :immutable-struct :ftype :defstruct))
(in-package :msdd)
(SETF *ARITY-CHECK-BY-TEST-CALL* NIL)
(defvar *num-processes*
    (read-from-string
     (uiop:run-program "cat /proc/cpuinfo | grep -c processor" :output :string)))
;; (setf *kernel* (make-kernel *num-processes*))

;;; set up the learning data

(defun read-as-bvs (csv)
  (iter (for line in-file csv using #'read-line)
        (for bv = (iter (for o in-stream (make-string-input-stream line))
                        (collect o result-type bit-vector)))
        (when (plusp (length bv))
          (collect bv))))

(defun bvs-array (bvs)
  "Stores the bit vectors into a single large bit vector, then
 returns an array displaced to the underlying bitvector"
  (let ((batch (length bvs))
        (w (/ (length (first bvs)) 2)))
    (let* ((arm (make-array (* batch 2 w)  :element-type 'bit))
           (a (make-array (list batch 2 w) :element-type 'bit :displaced-to arm)))
      (iter (for b below batch)
            (for bv in bvs)
            (replace arm bv :start1 (* b 2 w) :end1 (* (1+ b) 2 w)))
      a)))

;;; print the pddl domain

(defclass _nil () ())
(defun _nil () (make-instance '_nil))
(defmethod print-object ((object _nil) stream)
  (write-string "()" stream))

(defun sym (&rest args)
  (intern (format nil "狺狎珞┅ㄤ彐躅ㄤ轫癌蝈趱蝾骘蝽ㄚ癌犷犰殡澧扉篝簌溟愆┅ㄤ彐躅磲脲滹磲轭ㄤ狒岍戾舄è溟ǒ戾铉翳ㄦ轵篝溽翎┅博ㄤ轫ㄩ雉溟愆┅啜溴骈铄ㄤ镯衢灬翦铘ê蝈聃轵屙孱趔后趄轲侯彗狒轹瀛痱邈镱溟糸镱螬ê痱邃殂狒弩括磲疸狎＇溟眢┅括轸弪ㄦ矧趄犷箝糸镱轭溽翎ㄦ矧骝镯篚怏羼趄犷箝糸镱溟愆ㄦ矧麸篚怏羼趄犷箝糸镱溟愆ㄦ矧骝镯癌ㄣ镬戾泗啜横泗轱簌пㄡ痧禊＇泔钽狒孱狒篝蜷铉磲ъ轶＇痱轭悱麸篝蜷铉趄犷箝糸镱┅吼狎犴弭弪ㄟ铋飑辉夏虾审轸痫篌殁戾麸躞疳蜥礤翦蝮轭秕骝犴鬻矧肟吼蝈泔钿轸轱ㄡ钿括轸弪ㄦ矧轭溟眢ㄦ矧鲠祯轭鲥泗矧骝镯磲翥鲠祯ú铋飑ūㄣ镬戾泗洎┅òㄣ镬戾泗啜铒洎┅┅┅哄骀邈ㄡ钿括轸弪ㄦ矧轭溟眢ㄦ矧鲠祯灞轭鲥泗矧骝镯ㄦ矧鲠祯宀轭鲥泗矧麸ㄥ磲翥瑾鲠祯灞鲠祯宀è博铋飑è暴ㄣ镬戾泗洎┅è癌ㄣ镬戾泗啜铒洎┅è癌铋飑è暴铋飑è暴ㄣ镬戾泗洎┅è癌ㄣ镬戾泗啜铒洎┅┅┅┅┅┅换箦狎汨溽翎篝蝓泗躜ㄤ彐豉疱箧ī箝铉戾骒镝舂ㄤ彐豉疱骖īф轼铛愆ㄤ彐豉疱栳戽ī啜躅箝珙邃怡翦ūǒㄦ祜矧祜盹篝痫箝糸鲥骈铛博博┅ㄤ彐躅眭祠轸镫孱鏖漪瑭磲脲狎蜥鏖漪洪铋糸犰屐屙孱哄戾礤铘豉疱р轸横潢躞翎忪铋烘殪飙痫轭翦铋飑ㄤ彐躅泔瘗眭祠轸镫孱眙暴戾è眙眭祠轸镫孱戾铉翳眙暴┅蝈痨徙眙眙暴眙博ㄤ彐躅蝈聃轵邃īㄥ蝌矧㈨轶箝铉蝈聃轵邃狎珲礤铘┅ㄤ彐篝蝓泗铒溴换痱邈躜箫蝈聃轵邃呼疱箝眇戾狎蜥忾舂换篚沣弩箫蝈聃轵邃呼疱箝眇戾狎蜥忾舂ㄤ狒蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧磲箅蝈聃轵邃呼疱箝眇戾忾舡鲥泗矧┅ㄤ邈灬轫ㄩ铎轭铒溴痱邈躜箫颟ㄦ豉疱铒溴痱邈躜箫铒溴忾舡鲥泗矧ㄤ彐躅铒溴痱邈躜箫铒溴ㄥ磲翥铒溴è铒溴溽翎磲脲狎蜥ǒ戾铉翳溽翎博哄戾礤铘豉疱р轸轰轶痨徙邃麸溽翎┅┅ㄤ邈灬轫ㄩ铎轭铒溴痱邈躜箫颦┅ㄦ豉疱铒溴痱邈躜箫颦铒溴忾舡鲥泗矧ㄤ彐躅铒溴痱邈躜箫颦铒溴ㄥ磲翥铒溴è铒溴溽翎磲脲狎蜥ǒ戾铉翳溽翎博哄戾礤铘豉疱р轸轰轶痨徙邃麸溽翎┅┅ㄤ邈灬轫ㄩ铎轭铒溴篚沣弩箫颟ㄦ豉疱铒溴篚沣弩箫铒溴忾舡鲥泗矧ㄤ彐躅铒溴篚沣弩箫铒溴磲翥铒溴è铒溴溽翎戾è鏖漪ǒ戾铉翳溽翎博┅磲脲狎蜥鏖漪哄戾礤铘豉疱р轸轰轶痨徙邃麸溽翎轰轶痨徙邃轭溴镦骟弭鏖漪瑭┅┅ㄤ邈灬轫ㄩ铎轭铒溴篚沣弩箫颦┅ㄦ豉疱铒溴篚沣弩箫颦铒溴忾舡鲥泗矧ㄤ彐躅铒溴篚沣弩箫颦铒溴磲翥铒溴è铒溴溽翎戾è鏖漪ǒ戾铉翳溽翎博┅磲脲狎蜥鏖漪哄戾礤铘豉疱р轸轰轶痨徙邃麸溽翎轰轶痨徙邃轭溴镦骟弭鏖漪瑭┅┅ㄤ彐躅狍氨铒溴ㄥ磲翥铒溴è铒溴溽翎磲箅磲鲥泗矧灬礅溽磲翥瑾è暴博è癌洎溽翎磲箅┅┅换铒溴弼犰踽糸镱骢钽糸镱ㄤ邈灬轫ㄩ铎轭鏖汶孱蟓泔镢沲蝈钽瀛绌ㄦ豉疱鏖汶孱蟓泔镢沲蝈钽瀛蝈犰蝈犰蝈犰蝈犰箧ㄤ彐躅鏖汶孱蟓泔镢沲蝈钽瀛畋畈畛畲骑祆秣轭翳溴骈铋糸镱轭厢翦蟋蔑桢瞵擅吞苟渝犰箫组汶孱蟋援漠惫腹王祠轺狴蔑铘轭珏钽葬忪弩令犰箝骘翳语汩犰鱼殄钽弩提黩孱沐膨焘狨馏箫汩狒弩戾舄è畋ㄦ祜狒畋┅畈ㄦ祜狒畈┅畛ㄦ祜狒畛┅畲ㄦ祜狒畲┅ū犰ǒǐ畋畈畛畲┅虮ǐ畋畈┅ㄣǐ畋畛┅虿ǐ畛畲┅ㄣǐ畈畲┅畋í虮惚悲犰飑畈í虮悴悲犰飑畛í虿惚悲犰飑畲í虿悴悲犰飑┅í换徜溟铉卞麸痱弼孱溟鲩箝镱怡弪ǐí畋祜ǒǐ卞畋ǐ卞畋蕞┅í畈祜ǒǐ卞畈ǐ卞畈蕞┅í畛祜ǒǐ卞畛ǐ卞畛蕞┅í畲祜ǒǐ卞畲ǐ卞畲蕞┅┅┅ㄤ邈灬轫ㄩ铎轭泸矬蟓孱趄镳┅ㄦ豉疱泸矬蟓孱趄镳蝈犰蝈犰蝈犰蝈犰箧ㄤ彐躅泸矬蟓孱趄镳畋畈畛畲⒄箝铉翳轶轭篝遽镦鏖汶孱蟓泔镢沲蝈钽瀛忮汜躞翳轶鲠祯轶铒蝽犰辁邃徵衢铙翳麸翎筢眇戾箝瀹戾舄è畋ㄦ祜狒畋┅畈ㄦ祜狒畈┅畛ㄦ祜狒畛┅畲ㄦ祜狒畲┅ū犰ǒǐ畋畈畛畲┅虮ǐ畋畈┅ㄣǐ畋畛┅虿ǐ畛畲┅ㄣǐ畈畲┅畋í虮惚悲犰飑畈í虮悴悲犰飑畛í虿惚悲犰飑畲í虿悴悲犰飑┅í悲犰换徜溟铉卞麸痱弼孱溟鲩箝镱怡弪ǐí畋祜ǒǐ卞畋ǐ卞畋蕞┅í畈祜ǒǐ卞畈ǐ卞畈蕞┅í畛祜ǒǐ卞畛ǐ卞畛蕞┅í畲祜ǒǐ卞畲ǐ卞畲蕞┅┅┅ㄦ豉疱眢滗珥狲骖骖骖骖箧ㄤ彐躅眢滗珥狲畋畈畛畲磲ㄣ蝻篌孱趄镳畋畈ǐ畛畲┅ㄣ蝻篌孱趄镳ǐ畋畈畛畲┅ㄦ豉疱眢滗珥狲骖骖骖骖箧ㄤ彐躅眢滗珥狲畋畈畛畲磲戾è繇鸨ǐ畈畛畲┅ㄩ冀畋繇鸨ㄣ蝻篌孱趄镳畋繇鸨戾è犰殳ǒǐ畋繇鸨博┅ㄣ蝻篌孱趄镳犰殳犰殳博┅ㄣ镱è窘畋ㄡ怏ō畈畛┅戾è繇鸩ǐ畋畈畛┅ㄣ蝻篌孱趄镳繇鸩繇鸩畲┅è畈畛ㄣ蝻篌孱趄镳畈ǐ畋畛畲┅ㄣ蝻篌孱趄镳ǐ畋畈畛畲┅┅ㄦ豉疱眢滗珥狲铒溴骖骖骖骖箧ㄤ彐躅眢滗珥狲铒溴畋畈畛畲磲翥铒溴è铒溴磲箅ㄩ铒弪镳ㄣ秕铘磲箅后翎螋ǒ戾铉翳磲箅博┅换铒瞽屙痿篚沣弩箫眢滗珥狲畋畈畛畲换屙痿篚沣弩箫眢滗珥狲畋畈畛畲┅┅ㄤ彐磲泸滹糸礤蟓轭扉铄è鲠泔躅镳糸镱犰蝈篚祠骘蝽怙澌怙澌孱鲩蝻铐孱孱雯ㄣ桢汶豉疱鲠簌礅镬戾è泔躅磲泸镥疳钿泔躅孱雯┅ㄡ篌弪ㄡ钿ㄣ镱篝犷麴泔躅舂铛礅弪泔躅舂┅ㄩ翦ㄦ矧麸泔躅舂麒孱ㄦ轵篝轸弪狒轱瞽皓ㄣ镬戾泗ю蝻珙┅ㄣ镬戾泗ㄩ泔躅舂啜簌礅镬磲泸镬弭è鲠悌棱镤蝈篚祠骘蝽┅┅ㄤ彐磲泸滹糸礤蟓躅蝻祆è忉箦镦骟弭泔躅躅蝻祆镳糸镱犰蝈篚祠怙澌怙澌孱鲩蝻铐孱孱雯鏖翳珏铙眢聃狒盹铘璀祜镳戾è躅蝻祆磲泸镥疳钿躅蝻祆孱雯ㄤ屐翎ㄧ孱簌⒛┅啜戾è忉箦癌ㄤ邈灬蝈ㄦ轼铛忉箦┅眭祠轲戾鲠祯瀛忾钿ì聃狒盹洎ㄦ祜矧泔躅躅蝻祆ㄤ邈灬蝈ㄦ轼铛聃狒è盹躅蝻祆盹洎簌礅镬磲泸镬弭è溴祠躅蝻祆┅ㄤ雉轫弩ì铘璀祜镳聃狒ㄤ邈灬蝈ㄩ珙矧徕戾铘璀祜镳┅ㄤ雉轫弩轭扉铄ì镦骟弭躅蝻祆棱镤ㄩ钽忉箦溴祠岍┅簌礅镬磲泸镬弭è溴祠暴ì镦骟弭癌ㄤ雉轫弩ì铘璀祜镳盹洎棱镤ㄩ钽忉箦溴祠岍┅蝈篚祠┅┅ㄦ豉疱泔铘轭珏钽翎忪铒溴ㄡ蝌狴忾í┅鲠祯弩骖骖骖骖┅＋铋戾è磲翥栝铉磲脲狎蜥哄戾礤铘豉疱р轸┅痱邈磲箅磲脲狎蜥哄戾礤铘豉疱р轸┅篚沣磲箅磲脲狎蜥哄戾礤铘豉疱р轸┅ㄡ祆趄蹂磲脲狎蜥哄戾礤铘豉疱р轸┅繇鸨磲脲狎蜥哄戾礤铘豉疱р轸┅ㄤ邈灬蝈箝眇戾忾舡鲥泗矧磲翥栝铉痱邈磲箅篚沣磲箅犰飙趄蹂┅ㄤ彐躅泔铘轭珏钽翎忪铒溴狎蜥ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅ㄥ磲翥瑾ㄡ蝌狴铒溴èㄡ蝌狴轰轫孱箝镱扉篝忉翥鏖漪瑭轰轶痨徙邃麸忉汶狎蜥铒溴溽翎磲箅┅ㄤ邈灬蝈箝眇戾忾舡鲥泗矧忉汶狎蜥ㄨ犰鏖漪忉翥瑭戾舄è畋癌畈癌畛癌畲癌鏖漪璨í鏖漪瑭┅ㄤ邈灬蝈ㄦ轼铛畋畈畛畲ㄨ犰鏖漪璨┅麒孱ǒ鏖漪璨戾铉翳磲翥栝铉┅箦翩磲翥栝铉磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸痱邈磲箅磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸篚沣磲箅磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸犰飙趄蹂磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸繇鸨磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅ㄦ殪痱邈磲箅后翎螋哄钿鏖漪瑭ㄦ殪篚沣磲箅后翎螋鏖漪瑭ㄦ殪犰飙趄蹂暴ㄤ雉轫弩躅蝻祆ㄢ镦骟弭忉翥博戾è篝狎翳骈铛í翳骈铛ǐ镦骟弭┅鏖漪璨┅┅蝈痨徙繇鸨忉汶狎蜥后翎螋篝狎哄钿翳骈铛ǐ篝狎鏖漪璨┅┅换ㄤ雉轫弩ㄢ忉翥瑭换戾è篝狎í鏖漪璨┅换蝈痨徙繇鸨忉汶狎蜥后翎螋篝狎哄钿ǐ篝狎鏖漪璨┅ㄢ轸羼溽翎繇鸨磲翥栝铉ㄢ轸轱磲翥栝铉磲箅磲翥栝铉ㄥ磲翥瑾è羼踽犰飙趄蹂ㄢ轸轱磲翥栝铉篚沣磲箅繇鸨┅ㄥ聃犰犰飙趄蹂ㄢ轸轱磲翥栝铉痱邈磲箅繇鸨┅è舂ㄩ钽畋┅è铋飑ㄩ钽畈┅è铋舂ㄩ钽畛┅è铋铋飑ㄩ钽畲┅┅鲠祯弩畋畈畛畲┅┅┅ㄤ彐躅泔铘轭珏钽翎忪铒溴狎蜥ㄤ邈灬蝈镳糸黹箴邋畅筢驽豉癌┅ㄥ磲翥瑾ㄡ蝌狴铒溴èㄡ蝌狴轰轫孱箝镱扉篝忉翥鏖漪瑭轰轶痨徙邃麸忉汶狎蜥铒溴溽翎磲箅┅ㄤ邈灬蝈箝眇戾忾舡鲥泗矧忉汶狎蜥ㄨ犰鏖漪忉翥瑭戾舄è畋癌畈癌畛癌畲癌鏖漪璨í鏖漪瑭磲翥栝铉磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅痱邈磲箅磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅篚沣磲箅磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅ㄡ祆趄蹂磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅繇鸨磲脲狎蜥鏖漪璨哄戾礤铘豉疱р轸┅ㄤ邈灬蝈箝眇戾忾舡鲥泗矧磲翥栝铉痱邈磲箅篚沣磲箅犰飙趄蹂ㄦ轼铛畋畈畛畲ㄨ犰鏖漪璨┅ㄦ殪痱邈磲箅后翎螋哄钿鏖漪瑭ㄦ殪篚沣磲箅后翎螋鏖漪瑭ㄦ殪犰飙趄蹂暴ㄤ雉轫弩躅蝻祆ㄢ镦骟弭忉翥博戾è篝狎翳骈铛í翳骈铛ǐ镦骟弭┅鏖漪璨┅┅蝈痨徙繇鸨忉汶狎蜥后翎螋篝狎哄钿翳骈铛ǐ篝狎鏖漪璨┅┅ㄢ轸羼溽翎繇鸨磲翥栝铉ㄢ轸轱磲翥栝铉磲箅磲翥栝铉ㄥ磲翥瑾è羼踽犰飙趄蹂ㄢ轸轱磲翥栝铉篚沣磲箅繇鸨┅ㄥ聃犰犰飙趄蹂ㄢ轸轱磲翥栝铉痱邈磲箅繇鸨┅è舂ㄩ钽畋┅è铋飑ㄩ钽畈┅è铋舂ㄩ钽畛┅è铋铋飑ㄩ钽畲┅┅鲠祯弩畋畈畛畲┅┅换序躅轭蝓戾骘徙糸镱筱桢磲ㄤ彐躅鲠扉洵铒溴铒溴磲翥铒溴è铒溴痱邈躜箫篚沣弩箫痱邈躜箫颦篚沣弩箫颦换碑筢礤忾箬秕熹铒箴邈殒翳筢礤鲠祯鸫戾骠疳虺戾è繇鸨ㄢ轸羼痱邈躜箫篚沣弩箫颟泔眄镱忾趔繇鸩ㄢ轸矧痱邈躜箫痱邈躜箫颟┅狍弪ㄢ轸犷溷繇鸨痱邈躜箫颦繇鸨鏖熹骈祠弪铒瞽鏖熹ㄢ轸犷溷繇鸨篚沣弩箫颦繇鸨ㄥ聃犰繇鸩繇鸨犰弪换伯徙糸镱箬秕熹孱泔溴汨犷珏罂滹瞌翳轭翳轶轶铄沐篌狎┅┅ㄤ彐躅鲠扉洵戾徭铒溴铒溴磲翥铒溴è铒溴篚沣弩箫颦换钞翳弪箬秕熹忮箫礤彐驽泗鸫蜷玷衄扉铄抄穿铒ㄥ聃犰ㄢ轸羼篚沣弩箫颦篚沣弩箫颦狍镱篚沣弩箫颦┅┅换陀哪犰顼蜷翳ㄦ豉疱屮疳钿铒溴扉篝ㄤ彐躅屮疳钿铒溴磲翥铒溴è铒溴溽翎磲箅ㄦ戾è箴邈殒眭祠轸镫孱轭溴麒狒戾è眙ㄣ镳眭祠轸镫孱眭祠轸镫孱┅箦翩ㄡ蝈眙轭溴麒狒眙博┅换箴邈殒忾镦翳痱邈躜箫矧翳篚沣弩箫虍换娱钽翳痱邈躜箫犷翳篚沣弩箫狎翳溟箴灬沐狎蜥麸换箝铉戾躅溴蜢轭溽翎狎蜥换麇汜轸弪狒秭弪翳躅溴蜢轭狎蜥戾è蜷玷繇矬矧痫箝糸镱磲箅烘蝻憝孱舂暴┅ㄩ翦ㄦ矧骝镯ū蜷玷繇矬舂忮祜戾铉翳溽翎┅ㄣ镬戾泗铒溴箴邈殒溽翎癌箴邈殒磲箅癌┅ㄣ镬戾泗铒溴箴邈殒溽翎暴箴邈殒磲箅癌┅┅┅┅ㄤ彐躅痱镧蝈篌鏖漪瑭戾è泔躅癌灬礅溽é镳糸镱犰ㄣ栳＼蝈篝狎珞痱轭汨狎ㄩ钽泔躅舂麒孱鏖漪泔躅舂痱镧箦翩泔躅癌ㄦ矧磲狺%" args)))
      (finish-output))))

(defmacro print-variables (&rest args)
  `(print (list ,@(iter (for arg in args)
                        (collect `(quote ,arg))
                        (collect arg)))
          *error-output*))

;; it seems there are still some magic happening here... 

(defun bistate-msdd (array g-threashold max-schema)
  "A modification of Oates, Cohen, ICML16 on binary variables with lag fixed to 1.
Also, instead of working on the sequence of states, it works on individual pairs of states."
  (match array
    ((array :dimensions (list batch 2 width))
     (format *error-output* "number of samples: a%encoding length: a%" batch width)
     (let ((open (make-pqueue #'> :key-type 'sf :value-type 'node))
           (acc (make-pqueue #'< :key-type 'sf :value-type 'node))
           (lopen (bt:make-lock "open"))
           (lacc (bt:make-lock "acc"))
           threads)
       (labels ((task (node)
                  (block nil
                    ;; prune the child following Oates,Cohen,AAAI98
                    (unless (valid-node node) (return))
                    (multiple-value-bind (n1 n2 n3 n4)
                        (contingency-table node array)
                      (let ((g (cross-entropy n1 n2 n3 n4)))
                        (when (and (< g-threashold g)
                                   (valid-leaf-node node))
                          (bt:with-lock-held (lacc)
                            (when (or (< (pqueue-length acc) max-schema)
                                      (< (pqueue-front-key acc) g))
                              (pqueue-push node g acc))
                            (when (< max-schema (pqueue-length acc))
                              (pqueue-pop acc)))))
                      (let ((f (msdd-gmax3 node n1 n2 n3 n4)))
                        (when (and (< g-threashold f)
                                   (bt:with-lock-held (lacc)
                                     (or (< (pqueue-length acc) max-schema)
                                         (< (pqueue-front-key acc) f))))
                          (let ((children (expand node)))
                            (bt:with-lock-held (lopen)
                              (iter (for child in children)
                                    (pqueue-push child f open)))))))))
                (worker ()
                  (sleep (random 1.0))
                  (block nil
                    (tagbody
                      :start
                      (task
                       (bt:with-lock-held (lacc)
                         (bt:with-lock-held (lopen)
                           (if (or (< (pqueue-length acc) max-schema)
                                   (< (pqueue-front-key acc)
                                      (pqueue-front-key open)))
                               (if (pqueue-empty-p open)
                                   (go :retry)
                                   (pqueue-pop open))
                               (go :end)))))
                      (go :start)
                      :retry
                      (sleep 0.1)
                      (go :start)
                      :end))))
         (let ((init (node (multitoken (* 2 width))
                           (multitoken (* 2 width)))))
           (pqueue-push init
                        (multiple-value-call
                            #'cross-entropy
                          (contingency-table init array))
                        open))
         (dotimes (i *num-processes*)
           (push (bt:make-thread #'worker) threads))
         (unwind-protect
          (handler-case
              (map nil #'bt:join-thread threads)
            (SB-SYS:INTERACTIVE-INTERRUPT ()
              (map nil #'bt:destroy-thread threads)))
           (print-variables acc)))
       (iter (until (pqueue-empty-p acc))
             (for (values node g) = (pqueue-pop acc))
             (print-variables g node)
             (collect node))))))

(defvar *g-threshold* most-negative-short-float)
(defvar *max-schema* most-positive-fixnum)
(defvar *num-samples* nil)
(setf *print-right-margin* 70)
;; (setf *print-pretty* nil)
(defun main (&rest args)
  (match args
    ((list* (or "-g" "--threashold") (read *g-threshold*) rest)
     (check-type *g-threshold* real)
     (apply #'main rest))
    ((list* (or "-s" "--seed") (read seed) rest)
     (check-type seed (unsigned-byte 32))
     (let ((*random-state* (SB-EXT:SEED-RANDOM-STATE seed)))
       (apply #'main rest)))
    ((list* (or "-k" "--max-schema") (read *max-schema*) rest)
     (check-type *max-schema* integer)
     (apply #'main rest))
    ((list* (or "-n" "--num-samples") (read *num-samples*) rest)
     (check-type *num-samples* integer)
     (apply #'main rest))
    ((list* (or "-P" "--num-processes") (read *num-processes*) rest)
     (check-type *num-processes* integer)
     (apply #'main rest))
    ((list* (or "-p" "--profile") rest)
     (swank:profile-reset)
     (swank:profile-package :msdd t nil)
     (apply #'main rest)
     (swank:profile-report))
    ((list* (or "-t" "--time") rest)
     (let ((*trace-output* *error-output*))
       (time (apply #'main rest))))
    ((list csv)
     (format *error-output* "number of threads: a%" *num-processes*)
     (format
      t "&(:S)%"
      (make-domain
       (mapcar #'as-012
               (bistate-msdd
                (bvs-array
                 (if *num-samples*
                     (simpsamp:list-sample
                      *num-samples*
                      (let ((list (read-as-bvs csv)))
                        (format *error-output*
                                "number of original samples: a%"
                                (length list))
                        list))
                     (read-as-bvs csv)))
                *g-threshold* *max-schema*)))))
    (_
     (format *error-output*
             "Usage: msdd.ros                      [-g,--threashold threashold]                      [-p,--profile]                      [-t,--time] actions.csv%"))))

;;; vim: set ft=lisp lisp:




